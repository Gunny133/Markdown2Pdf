apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.de-swaef"
archivesBaseName = "Markdown2Pdf"
sourceCompatibility = 1.8
version = '1.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.easytesting:fest-assert:1.4'
    testCompile 'org.mockito:mockito-all:1.9.5'
    compile 'org.xhtmlrenderer:flying-saucer-pdf:9.0.4'
    compile 'org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.0'
    compile 'jtidy:jtidy:4aug2000r7-dev'
    compile 'org.apache.commons:commons-io:1.3.2'
}

task "forceTest" {
    dependsOn "cleanTest", "test"
}

task "fullBuild" {
    dependsOn "cleanTest", "test", "check", "build"
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Markdown2Pdf'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A library for converting Markdown to Pdf files'
                url 'https://github.com/Qkyrie/Markdown2Pdf'

                scm {
                    connection 'https://github.com/Qkyrie/Markdown2Pdf.git'
                    developerConnection 'https://github.com/Qkyrie/Markdown2Pdf.git'
                    url 'https://github.com/Qkyrie/Markdown2Pdf.git'
                }

                licenses {
                    license {
                        name 'GNU AFFERO GENERAL PUBLIC LICENSE'
                        url 'http://www.gnu.org/licenses/agpl.txt'
                    }
                }

                developers {
                    developer {
                        id 'Qkyrie'
                        name 'Quinten De Swaef'
                        email 'quintendeswaef@gmail.com'
                    }
                }
            }
        }
    }
}